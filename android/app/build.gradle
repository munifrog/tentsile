apply plugin: 'com.android.application'

// https://stackoverflow.com/q/35722904
def keysFile = rootProject.file("my.keys")
def keysProperties = new Properties()
keysProperties.load(new FileInputStream(keysFile))
def latestSdkVersion = 31

android {
    compileSdkVersion latestSdkVersion

    signingConfigs {
        release {
            storeFile file(keysProperties['release_store_file'])
            storePassword keysProperties['release_store_secret']
            keyPassword keysProperties['release_key_secret']
            keyAlias keysProperties['release_key_alias']
        }
    }

    defaultConfig {
        applicationId "com.munifrog.design.tetheredtenttriangulator"
        minSdkVersion 16
        targetSdkVersion latestSdkVersion
        versionCode 8
        versionName "1.6"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            shrinkResources true
            minifyEnabled true
            debuggable false
            jniDebuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "tier"
    productFlavors {
        lite {
            dimension "tier"
            applicationId 'com.munifrog.design.tetheredtenttriangulator.lite'
        }
        full {
            dimension "tier"
            applicationId 'com.munifrog.design.tetheredtenttriangulator.full'
        }
    }

    allprojects {
        // Uncomment to see Lint warnings
    /*
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    */
    }
}

dependencies {
    def app_compat_version = "1.4.2"
    def constraint_layout_version = "2.1.4"
    def espresso_core_version = "3.4.0"
    def junit_android_version = "1.1.3"
    def junit_java_version = "4.13.2"
    def preference_version = "1.2.0"

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "androidx.appcompat:appcompat:$app_compat_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"
    implementation "androidx.preference:preference:$preference_version"
    testImplementation "junit:junit:$junit_java_version"
    androidTestImplementation "androidx.test.ext:junit:$junit_android_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_core_version"
}